
 MODULE main
 VAR
    PC: 0..4;
    loop_count: 0..2;
    -- secret_in: 0..1;
    -- ndet_in: 0..1;
    var_x: 0..1;
    var_y: 0..1;
    obs_out: 0..2;


  ASSIGN
    -- L0: x := secret_input()
    -- L0: y := ndet_input()
    -- L1: if (x == y):
    -- L2:  out(x)
    -- L3: else: out(!x)
    -- L4: END
  init(PC):= 0;
  next(PC):=
    case
      (PC=0): {1};
      ((PC=1) & (var_x=var_y)): {2};
      ((PC=1) & (!(var_x=var_y))): {3};
      (PC=2): {4};
      (PC=3): {4};
      ((PC=4) & (loop_count<2)): {0}; -- loop back
      ((PC=4) & (loop_count=2)): {4}; -- terminate
      TRUE: PC;
    esac;

  init(loop_count) := 0;
  next(loop_count) :=
    case
      (loop_count=2): loop_count;
      (PC=0): loop_count+1;
      TRUE: loop_count;
    esac;

  init(obs_out) := 0;
  next(obs_out) :=
    case
      ((PC=4) & (loop_count=2)) : 2; --terminate
      (PC=2): var_x;
      ((PC=3)&(var_x=1)): 0;
      ((PC=3)&(var_x=0)): 1;
      TRUE: 2;
    esac;

  init(var_x) := {0, 1};
  next(var_x) :=
    case
      ((PC=4) & (loop_count=2)) : var_x; --terminate
      (PC=4) : {0, 1}; --re-fetch input
      TRUE: var_x;
    esac;

  init(var_y) := {0, 1};
  next(var_y) :=
    case
      ((PC=4) & (loop_count=2)) : var_y; --terminate
      (PC=4) : {0, 1}; --re-fetch input
      TRUE: var_y;
    esac;

  DEFINE
  halt := ((PC=4) & (loop_count=2));
